4.13 动态绑定：parameterize

parameterize表把一个新值和body表达式求值过程中的一个参数（parameter）相结合：

(parameterize ([parameter-expr value-expr] ...)
  body ...+)

例如，error-print-width参数控制在错误消息中打印一个值的字符数：

> (parameterize ([error-print-width 5])
    (car (expt 10 1024)))

car: contract violation
  expected: pair?
  given: 10...
  
> (parameterize ([error-print-width 10])
    (car (expt 10 1024)))

car: contract violation
  expected: pair?
  given: 1000000..
  
一般来说，参数实现了一种动态绑定。make-parameter函数接受任何值并返回一个初始化为给定值的新参数。应用参数作为一个函数返回它的其当前值：

> (define location (make-parameter "here"))
> (location)

"here"

parameterize表不是一个像let那样的绑定表；每次location的使用向上都直接指向原来的定义。在parameterize主体被求值的整个时间内parameterize表调整参数的值，甚至是文本以外的parameterize主体的参数使用：

> (define (would-you-could-you?)
    (and (not (equal? (location) "here"))
         (not (equal? (location) "there"))))
> (would-you-could-you?)

#f

> (parameterize ([location "on a bus"])
    (would-you-could-you?))

#t

……
